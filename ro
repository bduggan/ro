#!/usr/bin/env perl
use v5.20;
use Mojolicious::Lite;
use List::Util qw/max/;
use experimental 'signatures';
use lib '.';
use Room;

$ENV{MOJO_INACTVITIY_TIMEOUT} = 120;
sub debug($) { app->log->debug(@_) }

my %players;     # $id => $tx
my $room = Room->new;

app->hook(after_dispatch => sub($c) {
  $c->res->headers->header('Access-Control-Allow-Origin' => '*');
});

helper notify => sub ($c,$who,$what,$winner,$opponent) {
  return $players{$who}->send({ json => {
    you => 'tie', opponent => $opponent
  }}) if $winner eq 'tie';
  my $status = $what eq $winner ? 'win' : 'lose';
  $players{$who}->send({ json => {
    winner => $winner, you => $status, opponent => $opponent}});
};

helper new_player => sub($c) {
  state $i = 1;
  return $i++;
};

my %better_than = ( rock => 'scissors', paper => 'rock', 'scissors' => 'paper' );
helper winner => sub($c,$one,$two) {
  return $one if $better_than{$one} eq $two;
  return $two if $better_than{$two} eq $one;
  return 'tie';
};

helper go => sub ($c,$p1,$p2,$h1,$h2) {
  # play: player 1,2 hand 1,2
  my $winner = $c->winner($h1,$h2);
  $c->notify($p1, $h1, $winner, $p2);
  $c->notify($p2, $h2, $winner, $p1);
  $room->game_over($p1,$p2);
};

get '/' => sub ($c) {
  $c->render(text => "Welcome to ro.  Make a websocket connection to /ready.\n");
};

websocket '/ready' => sub($c) {
  # $us, $them are ids
  # $ours, $theirs are rock/paper/scissors
  $c->on(message => sub ($c,$msg) {
     $msg eq 'hello' or return debug 'did not say hello';
     my $us = $c->new_player;
     debug "New player: $us";
     my $tx = $c->tx;
     $tx->send({ json => { welcome => $us } });
     $players{$us} = $tx;
     $tx->unsubscribe('message');
     $tx->on(message => sub ($tx, $ours) {
       $ours =~ /^(rock|paper|scissors)$/
         or return $tx->send({ json => { error => "wat? ($ours)" } });
       debug "$us plays $ours";
       my $them;
       if ($them = $room->has_partner($us)) {
           $room->play($us, $ours);
           debug "$us already has partner $them";
           if (my $theirs = $room->played($them) ) {
               $c->go($us,$them,$ours,$theirs);
           } else {
               debug "waiting for play_by:$them";
               $room->on("play_by:$them" => sub ($tx, $theirs) {
                  $c->go($us,$them,$ours,$theirs);
                  $room->unsubscribe("play_by:$them");
               });
           }
       } else {
           debug "$us has no partner, entering the room";
           $room->enter($us, $ours);
           $room->on("unpaired_player", sub ($room, $them, $theirs) {
               $room->pair_up($us,$them) or return;
               debug "new pair : $us vs $them ($ours vs $theirs)";
               $c->go($us,$them,$ours,$theirs);
           });
       }
     });
  });
};

app->start;

__DATA__
@@ exception.html.ep
%== $exception
