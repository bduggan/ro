#!/usr/bin/env perl
use v5.20;
use Mojolicious::Lite;
use List::Util qw/max/;
use experimental 'signatures';
use lib '.';
use Room;
use Data::Dumper;
use Carp;

$ENV{MOJO_INACTIVITY_TIMEOUT} //= 10;

sub debug($) { app->log->debug(@_) }
sub error($) { app->log->error(@_) }
sub info($)  { app->log->info(@_) }

my %players;     # $id => $tx
my $room = Room->new(log => app->log);

app->plugin('Config', default => { timeout => 2 } );
my $conf = app->config;
$Game::TIMEOUT = $conf->{timeout};

app->hook(after_dispatch => sub($c) {
  $c->res->headers->header('Access-Control-Allow-Origin' => '*');
});

helper new_player => sub($c) {
  state $i = 1;
  return $i++;
};

helper notify => sub ($c,$us,$game) {
  my $ours = $game->played->{$us};
  my $them = $game->other_player($us);
  my $theirs = $game->played->{$them};
  my $status = $ours eq $theirs ? 'tie'
             : $ours eq $game->winner ? 'win'
             : 'lose';
  $players{$us}->send({
        json => {
            you      => $status,
            yours    => $ours,
            opponent => $them,
            theirs   => $theirs,
            game     => $game->id,
            pair     => $game->pair,
        }
    });
};

helper play => sub($c, $us, $ours, $game) {
  $game->shoot($us => $ours);
  $c->notify($_ => $game) for $game->players;
  $room->leave_game($_) for $game->players;
  info $game->summary;
};

get '/' => sub ($c) {
  $c->render(text => "Welcome to ro.  Make a websocket connection to /ready.\n");
};

get '/games' => sub ($c) {
  $c->render(json => { available => $room->available });
};

websocket '/ready' => sub($c) {
  $c->on(message => sub ($c,$msg) {
     $msg eq 'hello' or return do {
      error 'did not say hello';
      $c->tx->finish(1003);
     };

     # $us, $them are ids.  $ours, $theirs are rock/paper/scissors
     my $us = $c->new_player;
     debug "New player: $us";
     my $tx = $c->tx;
     $tx->send({ json => { welcome => $us } });
     $players{$us} = $tx;
     $tx->unsubscribe('message');

     $tx->on(finish => sub($tx,$id) {
       $room->leave($us);
     });

     # Establish connection but don't pair until we play.
     $tx->on(message => sub ($tx, $ours) {
       $ours =~ /^(rock|paper|scissors)$/
         or return $tx->send({ json => { error => "wat? ($ours)" } });
       $room->playing($us)
         and return $tx->send({json => { error => "only one hand at a time, please" } });

       debug "$us: $us plays $ours";
       # Do they have a partner?
       my $them = $room->has_partner($us) or do {
           debug "$us: no partner yet";
           # Is there a game in progress?
           my $game = $room->find_game($us, $ours) or return;
           debug "$us: found a game";
           # Pair and play.
           $game->pair( $room->pair_up($game->players, $us) );
           return $c->play($us, $ours, $game);
       };
       # Have a partner, did they play already?
       my $game = $room->playing($them)
           or return $room->add_game($us, $ours, $room->paired($us,$them));
       # Partner has played.
       $c->play($us, $ours, $game);
     });
  });
};

Mojo::IOLoop->singleton->reactor->on(error => sub {
  my ($io,$err) = @_;
  $err //= '(none)';
  error "error: $err";
  error Carp::longmess;
});

app->start;

__DATA__
@@ exception.html.ep
%== $exception

@@ not_found.html.ep
not found
